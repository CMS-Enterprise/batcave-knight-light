pipeline {
  agent {
    kubernetes {
      yaml """
      apiVersion: v1
      kind: Pod
      spec:
        restartPolicy: Never
        containers:
        - name: maven
          image: artifactory.cloud.cms.gov/docker/maven:3-amazoncorretto-21
          command: ['tail', '-f', '/dev/null']
      """
    }
  }

  options {
    copyArtifactPermission('*');
  }

  stages {
    stage('Build') {
      steps {
        container('maven') {
          dir('java-server') {
            sh 'mvn --no-transfer-progress package -DskipTests'
            archiveArtifacts artifacts: 'target/**/*.class', allowEmptyArchive: true
          }
        }
      }
    }

    stage('Test') {
      parallel {
        stage('Unit Test') {
          steps {
            container('maven') {
              dir('java-server') {
                sh 'mvn --no-transfer-progress test'
              }
            }
          }
        }
        stage('Lint') {
          steps {
            container('maven') {
              dir('java-server') {
                catchError(buildResult: 'SUCCESS', stageResult: 'UNSTABLE', message: '[WARNING] Coding style issues detected!') {
                  sh 'mvn --no-transfer-progress checkstyle:check'
                }
              }
            }
          }
        }
        stage('SAST') {
          steps {
            script {
              def sastParameters = [
                string(name: 'git_repository', value: "${scm.userRemoteConfigs[0].url}"),
                string(name: 'git_credentials', value: "${scm.userRemoteConfigs[0].credentialsId}"),
                string(name: 'git_commit', value: "${GIT_COMMIT}"),
                string(name: 'git_branch', value: "${env.GIT_BRANCH}"),
                string(name: 'copy_artifacts_job_name', value: "${env.JOB_NAME}"),
                string(name: 'copy_artifacts_build_number', value: "${env.BUILD_NUMBER}"),
                string(name: 'copy_artifacts_filter', value: 'java-server/target/**/*.class')
              ]

              if (env.CHANGE_ID) {
                sastParameters.add(string(name: 'git_change_id', value: "${env.CHANGE_ID}"))
              }

              build(job: 'Java Server SAST', wait: true, propagate: true, parameters: sastParameters)
            }
          }
        }
      }
    }

    stage('Delivery') {
      steps {
        build(job: 'Java Server Delivery', wait: true, propagate: true, parameters: [
          string(name: 'tag', value: "${GIT_COMMIT[0..7]}"),
          string(name: 'git_repository', value: "${scm.userRemoteConfigs[0].url}"),
          string(name: 'git_credentials', value: "${scm.userRemoteConfigs[0].credentialsId}"),
          string(name: 'git_commit', value: "${GIT_COMMIT}"),
          string(name: 'build_dir', value: 'java-server'),
          string(name: 'dockerfile', value: 'java-server/Dockerfile')
        ])
      }
    }
  }
}
